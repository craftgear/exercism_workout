<!DOCTYPE html>
<html lang="ja">
<head>
  <meta charset="UTF-8">
  <title>制約論理プログラミング超入門</title>
  <meta name="description" content="Prolog入門,制約論理プログラミング入門">
  <link rel="stylesheet" type="text/css" href="../home_styles.css">
</head>
<body><!-- geoguide start --><div align=center><script language="javascript">var jps=382116062;var jpt=1550881781</script><script language="javascript" src="http://bc-geocities.yahoo.co.jp/js/gg.js"></script></div><!-- geoguide end -->
M.Hiroi's Home Page<br>
<div class="small">
http://www.geocities.jp/m_hiroi/
</div>
<div class="ce">
<h1>Prolog Programming</h1>
<h2>制約論理プログラミング超入門</h2>
</div>
<div class="small">
[ <a href="../index.html">Home</a> | <a href="index.html">Prolog</a> | <a href="clp.html">C L P</a> ]
</div>
<hr>
<section class="contents">
<h4 id="chap22">●ライツアウト</h4>
<p> 今回はパズル「ライツアウト」を clpfd を使って解いてみましょう。パズルの説明は拙作のページ Prolog 入門: <a href="prolog11.html#chap33">整数の論理演算とビット操作</a> や Puzzle DE Programming: <a href="../puzzle/lightout.html">ライツアウトの解法</a> をお読みください。
</p>
<p> 下記 URL によると、ライツアウトの解法は連立一次方程式を解くことに帰着させることができるそうです。
</p>
<ul>
  <li><a href="http://www.ic-net.or.jp/home/takaken/nt/light/light2.html">驚異のライツアウト解法ロジック</a>, (高橋謙一郎さん)</li>
  <li><a href="http://www.ic-net.or.jp/home/takaken/nt/light/light3.html">ライツアウト(３カラーパズル)の計算解法</a>, (高橋謙一郎さん)</li>
  <li><a href="http://deepgreen.game.coocan.jp/old/shortnotes/lightsout/index.htm">ライツアウト ＆ ８めくりパズル</a>, (deepgreen さん)</li>
</ul>
<p> 具体的にいうと、5 行 5 列盤 N 色のライツアウトの解は、次に示す 25 本の連立方程式を解くことで求めることができます。
</p>
<pre class="item">
(X<sub>i</sub><sub>j-1</sub> + X<sub>i-1</sub><sub>j</sub> + X<sub>i</sub><sub>j</sub> + X<sub>i+1</sub><sub>j</sub> + X<sub>i</sub><sub>j+1</sub> + A<sub>i</sub><sub>j</sub>) mod N = 0, 0 &lt; i &lt;= 5, 0 &lt; j &lt;= 5
</pre>
<p> X<sub>i</sub><sub>j</sub> はボタン (i, j) を押す回数、A<sub>i</sub><sub>j</sub> はボタン (i, j) の状態 (色) を表します。たとえば、N = 2 でボタン (i, j) が点灯 (1) している場合、自身のボタンと周囲のボタンを押す回数が奇数回であれば、それを消灯することができます。逆に、消灯している場合であれば、ボタンを押す回数が偶数回であれば消灯したままになります。
</p>
<p> clpfd を使えば連立方程式をプログラムするだけで、N 色のライツアウトの解を求めることができます。5 行 5 列盤専用になりますが、プログラムは次のようになります。
</p>
<pre class="list">
リスト : ライツアウト (5 行 5 列盤) の解法

:- use_module(library(clpfd)).

make_list(0, _, []).
make_list(N, X, [X | Xs]) :-
    N > 0, N1 is N - 1, make_list(N1, X, Xs).

lo25(As, N, Xs) :-
    As = [A11, A12, A13, A14, A15,
          A21, A22, A23, A24, A25,
          A31, A32, A33, A34, A35,
          A41, A42, A43, A44, A45,
          A51, A52, A53, A54, A55],
    Xs = [X11, X12, X13, X14, X15,
          X21, X22, X23, X24, X25,
          X31, X32, X33, X34, X35,
          X41, X42, X43, X44, X45,
          X51, X52, X53, X54, X55],
    (      X11 + X12 + X21 + A11) mod N #= 0,
    (X11 + X12 + X13 + X22 + A12) mod N #= 0,
    (X12 + X13 + X14 + X23 + A13) mod N #= 0,
    (X13 + X14 + X15 + X24 + A14) mod N #= 0,
    (X14 + X15       + X25 + A15) mod N #= 0,

    (X11       + X21 + X22 + X31 + A21) mod N #= 0,
    (X12 + X21 + X22 + X23 + X32 + A22) mod N #= 0,
    (X13 + X22 + X23 + X24 + X33 + A23) mod N #= 0,
    (X14 + X23 + X24 + X25 + X34 + A24) mod N #= 0,
    (X15 + X24 + X25       + X35 + A25) mod N #= 0,

    (X21       + X31 + X32 + X41 + A31) mod N #= 0,
    (X22 + X31 + X32 + X33 + X42 + A32) mod N #= 0,
    (X23 + X32 + X33 + X34 + X43 + A33) mod N #= 0,
    (X24 + X33 + X34 + X35 + X44 + A34) mod N #= 0,
    (X25 + X34 + X35       + X45 + A35) mod N #= 0,

    (X31       + X41 + X42 + X51 + A41) mod N #= 0,
    (X32 + X41 + X42 + X43 + X52 + A42) mod N #= 0,
    (X33 + X42 + X43 + X44 + X53 + A43) mod N #= 0,
    (X34 + X43 + X44 + X45 + X54 + A44) mod N #= 0,
    (X35 + X44 + X45       + X55 + A45) mod N #= 0,

    (      X51 + X52 + X41 + A51) mod N #= 0,
    (X51 + X52 + X53 + X42 + A52) mod N #= 0,
    (X52 + X53 + X54 + X43 + A53) mod N #= 0,
    (X53 + X54 + X55 + X44 + A54) mod N #= 0,
    (X54 + X55       + X45 + A55) mod N #= 0,

    M is N - 1,
    Xs ins 0 .. M,
    label(Xs).

solver(N, As) :-
    lo25(As, N, Xs), write(Xs), sum_list(Xs, M), write(M), nl, fail.
</pre>
<p> プログラムは簡単なので説明は割愛します。それでは実際に試してみましょう。すべてのボタンの色が N - 1 の状態で、ライトを消灯する場合の解は次のようになります。
</p>
<pre>
?- make_list(25, 1, As), solver(2, As).
[0,0,0,1,1,1,1,0,1,1,1,1,1,0,0,0,1,1,1,0,1,0,1,1,0]15
[0,1,1,0,1,0,1,1,1,0,0,0,1,1,1,1,1,0,1,1,1,1,0,0,0]15
[1,0,1,1,0,0,1,1,1,0,1,1,1,0,0,1,1,0,1,1,0,0,0,1,1]15
[1,1,0,0,0,1,1,0,1,1,0,0,1,1,1,0,1,1,1,0,0,1,1,0,1]15
false.

?- make_list(25, 2, As), solver(3, As).
[0,0,0,2,2,1,1,2,0,0,2,0,1,0,2,1,0,1,1,2,1,2,1,0,2]24
[0,0,1,2,0,1,0,1,1,2,0,2,1,1,1,1,1,2,0,0,2,1,0,1,0]21
[0,0,2,2,1,1,2,0,2,1,1,1,1,2,0,1,2,0,2,1,0,0,2,2,1]27
[0,1,0,1,2,0,0,2,1,1,1,1,1,2,0,2,1,1,0,1,0,2,1,0,0]21
[0,1,1,1,0,0,2,1,2,0,2,0,1,0,2,2,2,2,2,2,1,1,0,1,1]27
[0,1,2,1,1,0,1,0,0,2,0,2,1,1,1,2,0,0,1,0,2,0,2,2,2]24
[0,2,0,0,2,2,2,2,2,2,0,2,1,1,1,0,2,1,2,0,2,2,1,0,1]30
[0,2,1,0,0,2,1,1,0,1,1,1,1,2,0,0,0,2,1,1,0,1,0,1,2]21
[0,2,2,0,1,2,0,0,1,0,2,0,1,0,2,0,1,0,0,2,1,0,2,2,0]21
[1,0,0,2,1,0,0,2,1,1,0,2,1,1,1,2,1,1,0,1,1,1,1,1,2]24
[1,0,1,2,2,0,2,1,2,0,1,1,1,2,0,2,2,2,2,2,2,0,0,2,0]30
[1,0,2,2,0,0,1,0,0,2,2,0,1,0,2,2,0,0,1,0,0,2,2,0,1]21
[1,1,0,1,1,2,2,2,2,2,2,0,1,0,2,0,2,1,2,0,0,1,1,1,0]27
[1,1,1,1,2,2,1,1,0,1,0,2,1,1,1,0,0,2,1,1,1,0,0,2,1]24
[1,1,2,1,0,2,0,0,1,0,1,1,1,2,0,0,1,0,0,2,2,2,2,0,2]24
[1,2,0,0,1,1,1,2,0,0,1,1,1,2,0,1,0,1,1,2,2,1,1,1,1]24
[1,2,1,0,2,1,0,1,1,2,2,0,1,0,2,1,1,2,0,0,0,0,0,2,2]24
[1,2,2,0,0,1,2,0,2,1,0,2,1,1,1,1,2,0,2,1,1,2,2,0,0]27
[2,0,0,2,0,2,2,2,2,2,1,1,1,2,0,0,2,1,2,0,1,0,1,2,2]30
[2,0,1,2,1,2,1,1,0,1,2,0,1,0,2,0,0,2,1,1,2,2,0,0,0]24
[2,0,2,2,2,2,0,0,1,0,0,2,1,1,1,0,1,0,0,2,0,1,2,1,1]24
[2,1,0,1,0,1,1,2,0,0,0,2,1,1,1,1,0,1,1,2,0,0,1,2,0]21
[2,1,1,1,1,1,0,1,1,2,1,1,1,2,0,1,1,2,0,0,1,2,0,0,1]24
[2,1,2,1,2,1,2,0,2,1,2,0,1,0,2,1,2,0,2,1,2,1,2,1,2]33
[2,2,0,0,0,0,0,2,1,1,2,0,1,0,2,2,1,1,0,1,2,0,1,2,1]24
[2,2,1,0,1,0,2,1,2,0,0,2,1,1,1,2,2,2,2,2,0,2,0,0,2]30
[2,2,2,0,2,0,1,0,0,2,1,1,1,2,0,2,0,0,1,0,1,1,2,1,0]24
false.

?- make_list(25, 3, As), solver(4, As).
[0,0,2,1,3,1,3,2,3,1,1,3,3,2,2,0,3,3,3,0,1,0,1,1,0]39
[0,1,1,0,1,0,3,3,3,0,2,2,3,3,1,1,3,2,3,1,3,1,2,0,0]39
[0,2,0,3,3,3,3,0,3,3,3,1,3,0,0,2,3,1,3,2,1,2,3,3,0]47
[0,3,3,2,1,2,3,1,3,2,0,0,3,1,3,3,3,0,3,3,3,3,0,2,0]47
[1,0,1,1,0,0,3,3,3,0,1,3,3,2,2,1,3,2,3,1,0,0,2,1,3]39
[1,1,0,0,2,3,3,0,3,3,2,2,3,3,1,2,3,1,3,2,2,1,3,0,3]47
[1,2,3,3,0,2,3,1,3,2,3,1,3,0,0,3,3,0,3,3,0,2,0,3,3]47
[1,3,2,2,2,1,3,2,3,1,0,0,3,1,3,0,3,3,3,0,2,3,1,2,3]47
[2,0,0,1,1,3,3,0,3,3,1,3,3,2,2,2,3,1,3,2,3,0,3,1,2]47
[2,1,3,0,3,2,3,1,3,2,2,2,3,3,1,3,3,0,3,3,1,1,0,0,2]47
[2,2,2,3,1,1,3,2,3,1,3,1,3,0,0,0,3,3,3,0,3,2,1,3,2]47
[2,3,1,2,3,0,3,3,3,0,0,0,3,1,3,1,3,2,3,1,1,3,2,2,2]47
[3,0,3,1,2,2,3,1,3,2,1,3,3,2,2,3,3,0,3,3,2,0,0,1,1]47
[3,1,2,0,0,1,3,2,3,1,2,2,3,3,1,0,3,3,3,0,0,1,1,0,1]39
[3,2,1,3,2,0,3,3,3,0,3,1,3,0,0,1,3,2,3,1,2,2,2,3,1]47
[3,3,0,2,0,3,3,0,3,3,0,0,3,1,3,2,3,1,3,2,0,3,3,2,1]47
false.
</pre>
<p> 解が存在する場合、その総数は 2 色で 4 通り、3 色で 27 通り、4 色で 16 通りあります。なお、色数を増やすと実行時間がかかることに注意してください。
</p>
<h4 id="chap23">●m 行 n 列盤の解法</h4>
<p> 次は、ライトの状態を消灯・点灯に限定して、盤面の大きさを変更してみましょう。この場合、連立方程式を生成するプログラムを作ったほうが簡単です。次のリストを見てください。
</p>
<pre class="list">
リスト : ライツアウト (m 行 n 列盤) の解法

:- use_module(library(clpfd)).

% 要素が X で個数が N のリストを生成する
make_list(0, _, []).
make_list(N, X, [X | Xs]) :-
    N &gt; 0, N1 is N - 1, make_list(N1, X, Xs).

% 反転するボタンの位置を求める
reverse_buttons(_, _, _, _, [], []).
reverse_buttons(X, Y, L, C, [[Dx, Dy] | Ds], [Z | Zs]) :-
    X1 is X + Dx,
    Y1 is Y + Dy,
    X1 &gt;= 0,
    X1 &lt; C,
    Y1 &gt;= 0,
    Y1 &lt; L,
    Z is Y1 * C + X1,
    !,
    reverse_buttons(X, Y, L, C, Ds, Zs).
reverse_buttons(X, Y, L, C, [_ | Ds1], Zs) :- reverse_buttons(X, Y, L, C, Ds1, Zs).

% 式を組み立てる
make_expr(_, A, [], N) :- (N + A) mod 2 #= 0.
make_expr(Xs, A, [I | Is], N) :-
    nth0(I, Xs, V),
    N1 #= N + V,
    make_expr(Xs, A, Is, N1).

make_expr(Xs, A, [I | Is]) :-
    nth0(I, Xs, V),
    make_expr(Xs, A, Is, V).

% 規則 (連立方程式) を設定する
make_rule(N, L, C, _, _, _) :- N =:= L * C.
make_rule(N, L, C, Ds, Xs, [A | As]) :-
    X is N mod C,
    Y is N // C,
    reverse_buttons(X, Y, L, C, Ds, Is),
    make_expr(Xs, A, Is),
    N1 is N + 1,
    make_rule(N1, L, C, Ds, Xs, As).

% ライツアウトの解法
lo(L, C, As, Xs) :-
    K is L * C,
    length(Xs, K),
    make_rule(0, L, C, [[0,-1],[-1,0],[0,0],[1,0],[0,1]], Xs, As),
    Xs ins 0..1,
    label(Xs).

solver_lo(L,C) :-
    K is L * C,
    make_list(K, 1, Xs),
    lo(L, C, Xs, As),
    sum_list(As, N),
    write(As),
    write(N),
    nl,
    fail.
</pre>
<p> ライツアウトの解法は述語 lo(L, C, Xs, As) で行います。L が行数 (lines)、C が列数 (columns) です。Xs はボタンを表す変数を格納したリスト、As がボタンの状態 (0 or 1) を格納したリストです。最初に length で K 個の変数を生成し、述語 make_rule で連立方程式を設定します。第 4 引数はボタン (X, Y) と反転するボタンの差分を格納したリストです。差分から反転するボタンの位置を述語 reverse_buttons で求めます。そして、述語 make_expr でボタン (X, Y) の式を生成します。最後の solver_lo はすべてのボタンが点灯している状態の解を求める述語です。
</p>
<p> それでは実際に試してみましょう。
</p>
<pre>
?- solver_lo(3, 3).
[1,0,1,0,1,0,1,0,1]5
false.

?- solver_lo(4, 4).
[0,0,0,0,1,1,1,1,1,0,0,1,1,1,1,1]10
[0,0,0,1,1,1,0,0,1,1,0,0,0,0,0,1]6
[0,0,1,0,1,0,0,0,0,0,0,1,0,1,0,0]4
[0,0,1,1,1,0,1,1,0,1,0,0,1,0,1,0]8
[0,1,0,0,0,0,0,1,1,0,0,0,0,0,1,0]4
[0,1,0,1,0,0,1,0,1,1,0,1,1,1,0,0]8
[0,1,1,0,0,1,1,0,0,0,0,0,1,0,0,1]6
[0,1,1,1,0,1,0,1,0,1,0,1,0,1,1,1]10
[1,0,0,0,0,0,1,1,0,0,1,1,1,0,0,0]6
[1,0,0,1,0,0,0,0,0,1,1,0,0,1,1,0]6
[1,0,1,0,0,1,0,0,1,0,1,1,0,0,1,1]8
[1,0,1,1,0,1,1,1,1,1,1,0,1,1,0,1]12
[1,1,0,0,1,1,0,1,0,0,1,0,0,1,0,1]8
[1,1,0,1,1,1,1,0,0,1,1,1,1,0,1,1]12
[1,1,1,0,1,0,1,0,1,0,1,0,1,1,1,0]10
[1,1,1,1,1,0,0,1,1,1,1,1,0,0,0,0]10
false.

?- solver_lo(5, 5).
[0,0,0,1,1,1,1,0,1,1,1,1,1,0,0,0,1,1,1,0,1,0,1,1,0]15
[0,1,1,0,1,0,1,1,1,0,0,0,1,1,1,1,1,0,1,1,1,1,0,0,0]15
[1,0,1,1,0,0,1,1,1,0,1,1,1,0,0,1,1,0,1,1,0,0,0,1,1]15
[1,1,0,0,0,1,1,0,1,1,0,0,1,1,1,0,1,1,1,0,0,1,1,0,1]15
false.

?- solver_lo(6, 6).
[1,0,1,1,0,1,0,1,1,1,1,0,1,1,1,1,1,1,1,1,1,1,1,1,0,1,1,1,1,0,1,0,1,1,0,1]28
false.

?- solver_lo(7, 7).
[1,1,0,1,0,1,1,1,1,1,0,1,1,1,0,1,1,0,1,1,0,1,0,0,1,0,0,1,0,1,1,0,1,1,0,1,
 1,1,0,1,1,1,1,1,0,1,0,1,1]33
false.

?- solver_lo(8, 8).
[1,1,0,0,0,0,1,1,1,1,0,1,1,0,1,1,0,0,1,1,1,1,0,0,0,1,1,1,1,1,1,0,0,1,1,1,
 1,1,1,0,0,0,1,1,1,1,0,0,1,1,0,1,1,0,1,1,1,1,0,0,0,0,1,1]40
</pre>
<p> <a href="http://d.hatena.ne.jp/tnkysr/20060510/p1">日月離反 - atobirabon</a> によると、<cite>『どんなm×nライツアウトも，すべてのライトがonのときは必ず解ける．』</cite> とのことです。ライツアウトの数学的な解析を公開されている作者様に感謝いたします。また、解が一つしかない場合はライトがどんなパターンでも解くことができ、複数ある場合は解けないパターンが存在します。たとえば 4 行 4 列盤の場合、パターンの総数は 2<sup>16</sup> 通りありますが、16 通りの複数解があるので、解けるパターンは 65536 / 16 = 4096 通りしかありません。
</p>
<p> なお、盤のサイズを大きくすると時間がかかるようになります。clpfd を使うと簡単にプログラムできますが、今回のプログラムの実行速度は遅いです。ご注意くださいませ。
</p>
<h4 id="chap24">●反転パターンの変更</h4>
<p> 次は拙作のページ Puzzle DE Programming: <a href="../puzzle/lightout2.html">変形版「ライツアウト」</a> で取り上げた、反転パターンを X にしてみましょう。プログラムは簡単です。次のリストを見てください。
</p>
<pre class="list">
リスト : 変形版ライツアウト

xlo(L, C, As, Xs) :-
    K is L * C,
    length(Xs, K),
    make_rule(0, L, C, [[-1,-1],[1,-1],[0,0],[-1,1],[1,1]], Xs, As),
    Xs ins 0..1,
    label(Xs).

solver_xlo(L,C) :-
    K is L * C,
    make_list(K, 1, Xs),
    xlo(L, C, Xs, As),
    sum_list(As, N),
    write(As),
    write(N),
    nl,
    fail.
</pre>
<p> 述語 xlo で make_rule を呼び出すとき、反転するボタンの位置を X の形に修正するだけです。それでは実行してみましょう。
</p>
<pre>
?- solver_xlo(3, 3).
[0,1,0,1,1,1,0,1,0]5
false.

?- solver_xlo(4, 4).
[0,0,0,0,0,1,1,0,1,1,1,1,0,1,1,0]8
[0,0,0,1,1,1,0,0,1,0,1,0,1,1,1,0]8
[0,0,1,0,0,1,1,1,0,1,1,1,0,0,1,0]8
[0,0,1,1,1,1,0,1,0,0,1,0,1,0,1,0]8
[0,1,0,0,1,1,1,0,1,1,1,0,0,1,0,0]8
[0,1,0,1,0,1,0,0,1,0,1,1,1,1,0,0]8
[0,1,1,0,1,1,1,1,0,1,1,0,0,0,0,0]8
[0,1,1,1,0,1,0,1,0,0,1,1,1,0,0,0]8
[1,0,0,0,0,0,1,1,0,1,0,1,0,1,1,1]8
[1,0,0,1,1,0,0,1,0,0,0,0,1,1,1,1]8
[1,0,1,0,0,0,1,0,1,1,0,1,0,0,1,1]8
[1,0,1,1,1,0,0,0,1,0,0,0,1,0,1,1]8
[1,1,0,0,1,0,1,1,0,1,0,0,0,1,0,1]8
[1,1,0,1,0,0,0,1,0,0,0,1,1,1,0,1]8
[1,1,1,0,1,0,1,0,1,1,0,0,0,0,0,1]8
[1,1,1,1,0,0,0,0,1,0,0,1,1,0,0,1]8
false.

?- solver_xlo(5, 5).
[0,0,0,0,1,0,1,1,0,0,1,1,1,1,1,0,1,1,0,0,0,0,0,0,1]11
[0,0,0,1,1,1,1,0,0,0,1,0,1,0,1,0,1,0,0,1,0,1,0,0,1]11
[0,0,1,0,0,0,1,1,1,0,0,1,1,1,0,0,0,1,0,0,1,0,1,0,1]11
[0,0,1,1,0,1,1,0,1,0,0,0,1,0,0,0,0,0,0,1,1,1,1,0,1]11
[0,1,0,0,1,0,1,0,0,1,1,0,1,0,1,1,1,0,0,0,0,0,0,1,1]11
[0,1,0,1,1,1,1,1,0,1,1,1,1,1,1,1,1,1,0,1,0,1,0,1,1]19
[0,1,1,0,0,0,1,0,1,1,0,0,1,0,0,1,0,0,0,0,1,0,1,1,1]11
[0,1,1,1,0,1,1,1,1,1,0,1,1,1,0,1,0,1,0,1,1,1,1,1,1]19
[1,0,0,0,0,0,0,1,1,0,1,1,1,1,1,0,0,1,1,0,1,0,0,0,0]11
[1,0,0,1,0,1,0,0,1,0,1,0,1,0,1,0,0,0,1,1,1,1,0,0,0]11
[1,0,1,0,1,0,0,1,0,0,0,1,1,1,0,0,1,1,1,0,0,0,1,0,0]11
[1,0,1,1,1,1,0,0,0,0,0,0,1,0,0,0,1,0,1,1,0,1,1,0,0]11
[1,1,0,0,0,0,0,0,1,1,1,0,1,0,1,1,0,0,1,0,1,0,0,1,0]11
[1,1,0,1,0,1,0,1,1,1,1,1,1,1,1,1,0,1,1,1,1,1,0,1,0]19
[1,1,1,0,1,0,0,0,0,1,0,0,1,0,0,1,1,0,1,0,0,0,1,1,0]11
[1,1,1,1,1,1,0,1,0,1,0,1,1,1,0,1,1,1,1,1,0,1,1,1,0]19
false.

?- solver_xlo(6, 6).
[0,0,1,1,0,0,0,1,1,1,1,0,1,1,0,0,1,1,1,1,0,0,1,1,0,1,1,1,1,0,0,0,1,1,0,0]20
false.

?- solver_xlo(7, 7).
[1,1,0,1,0,1,1,1,0,1,1,1,0,1,0,1,0,1,0,1,0,1,1,1,1,1,1,1,0,1,0,1,0,1,0,1,
 0,1,1,1,0,1,1,1,0,1,0,1,1]33
false.

?- solver_xlo(8, 8).
[0,0,1,0,0,1,0,0,1,1,0,1,1,0,1,1,1,0,0,1,1,0,0,1,1,1,1,0,0,1,1,1,0,0,1,1,
 1,1,0,0,1,0,0,1,1,0,0,1,0,0,0,0,0,0,0,0,1,1,1,1,1,1,1,1]34
[0,1,1,1,0,1,0,1,0,1,0,1,0,0,0,1,1,0,0,1,1,0,0,1,0,1,1,0,1,1,0,1,0,1,1,0,
 1,1,0,1,1,0,0,1,1,0,0,1,0,1,0,1,0,0,0,1,0,1,1,1,0,1,0,1]34
[1,0,1,0,1,1,1,0,1,0,0,0,1,0,1,0,1,0,0,1,1,0,0,1,1,0,1,1,0,1,1,0,1,0,1,1,
 0,1,1,0,1,0,0,1,1,0,0,1,1,0,0,0,1,0,1,0,1,0,1,0,1,1,1,0]34
[1,1,1,1,1,1,1,1,0,0,0,0,0,0,0,0,1,0,0,1,1,0,0,1,0,0,1,1,1,1,0,0,1,1,1,0,
 0,1,1,1,1,0,0,1,1,0,0,1,1,1,0,1,1,0,1,1,0,0,1,0,0,1,0,0]34
false.
</pre>
<p> 5 行 5 列盤の場合、<a href="../puzzle/lightout2.html">変形版「ライツアウト」</a> の解析では、最長手数が 13 手で、解けるパターンは 2<sup>25</sup> / 16 = 2097152 通りありました。解が 16 通り見つかったので、この解析結果と一致しています。盤面のサイズを変えて試してみたところ、全点灯のパターンは解けるようですね。ただし、証明はしていないので、必ず解けると断定することはできません。興味のある方は証明にも挑戦してみてください。
</p>
<h4 id="chap25">●８めくりパズル</h4>
<p> 「８めくり」はライツアウトに類似のパズルです。ルールは簡単で、あるボタンを押すと周囲のボタンの状態が反転します。つまり、光っているボタンは消灯し、消えていたボタンは点灯します。次の図を見てください。
</p>
<pre class="fig">
０１２
３４５  ボタンの番号
６７８

□□□      ■■■    □□□      □■□    □□□      ■□■
□□□ ←→ ■□■    □□□ ←→ ■■□    □□□ ←→ ■■■
□□□      ■■■    □□□      □□□    □□□      □□□

    ４を押す               ０を押す              １を押す

                    図 : 反転パターン
</pre>
<p> 中央のボタン 4 を押すと、その周囲のボタン 8 個の状態が反転します。押したボタンの状態は反転しません。もう一度同じボタンを押すと、再度ボタンの状態が反転するので、元の状態に戻ります。隅のボタン 0 を押すと 3 個のボタンの状態が反転し、辺にあるボタン 1 を押すと 5 個のボタンの状態が反転します。
</p>
<p> ８めくりの解法プログラムも簡単に作ることができます。プログラムと実行結果を示します。
</p>
<pre class="list">
 リスト : ８めくりパズルの解法

turnover(L, C, As, Xs) :-
    K is L * C,
    length(Xs, K),
    make_rule(0, L, C, [[-1,-1],[0,-1],[1,-1],[-1,0],[1,0],[-1,1],[0,1],[1,1]], Xs, As),
    Xs ins 0..1,
    label(Xs).

solver_turnover(L,C) :-
    K is L * C,
    make_list(K, 1, Xs),
    turnover(L, C, Xs, As),
    sum_list(As, N),
    write(As),
    write(N),
    nl,
    fail.
</pre>
<pre>
?- solver_turnover(4, 4).
[0,0,0,0,1,0,0,1,0,1,1,0,1,0,0,1]6
[0,0,0,1,0,1,0,1,0,0,1,1,1,1,1,0]8
[0,0,1,0,0,1,1,1,1,1,1,0,0,1,0,0]8
[0,0,1,1,1,0,1,1,1,0,1,1,0,0,1,1]10
[0,1,0,0,1,1,1,0,0,1,1,1,0,0,1,0]8
[0,1,0,1,0,0,1,0,0,0,1,0,0,1,0,1]6
[0,1,1,0,0,0,0,0,1,1,1,1,1,1,1,1]10
[0,1,1,1,1,1,0,0,1,0,1,0,1,0,0,0]8
[1,0,0,0,1,0,1,0,1,1,0,0,0,1,1,1]8
[1,0,0,1,0,1,1,0,1,0,0,1,0,0,0,0]6
[1,0,1,0,0,1,0,0,0,1,0,0,1,0,1,0]6
[1,0,1,1,1,0,0,0,0,0,0,1,1,1,0,1]8
[1,1,0,0,1,1,0,1,1,1,0,1,1,1,0,0]10
[1,1,0,1,0,0,0,1,1,0,0,0,1,0,1,1]8
[1,1,1,0,0,0,1,1,0,1,0,1,0,0,0,1]8
[1,1,1,1,1,1,1,1,0,0,0,0,0,1,1,0]10
false.

?- solver_turnover(5, 5).
false.

?- solver_turnover(6, 6).
[1,0,0,0,0,1,
 0,1,1,1,1,0,
 0,1,0,0,1,0,
 0,1,0,0,1,0,
 0,1,1,1,1,0,
 1,0,0,0,0,1]16
false.

?- solver_turnover(7, 7).
false.

?- solver_turnover(8, 8).
[0,1,1,0,0,1,1,0,
 1,1,0,1,1,0,1,1,
 1,0,1,0,0,1,0,1,
 0,1,0,0,0,0,1,0,
 0,1,0,0,0,0,1,0,
 1,0,1,0,0,1,0,1,
 1,1,0,1,1,0,1,1,
 0,1,1,0,0,1,1,0]32
false.

?- solver_turnover(9, 9).
false.

?- solver_turnover(10, 10).
[1,0,1,1,1,1,1,1,0,1,
 0,1,0,1,0,0,1,0,1,0,
 1,0,0,1,1,1,1,0,0,1,
 1,1,1,0,0,0,0,1,1,1,
 1,0,1,0,1,1,0,1,0,1,
 1,0,1,0,1,1,0,1,0,1,
 1,1,1,0,0,0,0,1,1,1,
 1,0,0,1,1,1,1,0,0,1,
 0,1,0,1,0,0,1,0,1,0,
 1,0,1,1,1,1,1,1,0,1]60
false.
</pre>
<p> 6 * 6 盤, 8 * 8 盤, 10 * 10 盤の結果は手作業で整形しています。ライツアウトと違って、盤のサイズによっては全点灯パターンが解けないこともあります。興味のある方はいろいろ試してみてください。
</p>
<p> ところで、拙作のページ Scheme Programming: <a href="../func/abcscm38.html#answer4">パズルに挑戦 ８めくりの解答</a> で最長手数を求めたことがあります。4 * 4 盤の場合、最長手数は 7 手で、局面の総数は全部のボタンが消灯した状態を含めて 4096 通りになりました。全局面の 1 / 16 しかありません。4 * 6 盤の場合、最長手数は 24 手で、全局面数は 2 ^ 24 = 16777216 通りになります。5 * 5 盤の場合、全消灯に到達できる局面は 2 ^ 25 / 2 = 16777216 通りあり、その中で最長手数は 20 手 (126 通り) になります。
</p>
<p> なお、盤面のサイズを大きくすると、clpfd では時間がかかるようになります。<a href="http://deepgreen.game.coocan.jp/old/shortnotes/lightsout/index.htm">deepgreen さん</a> が作成された解法プログラム (turnover3.exe) を使うと、32 * 32 盤でも 94 msec (Windows 10, Intel Core i5-6200U 2.30GHz) で解くことができます。ソースコードも公開されているので、興味のある方は deepgreen さんの Web ページをお読みください。deepgreen さんに感謝いたします。
</p>
</section>
<hr>
<div class="ce">
<b>Copyright (C) 2018 Makoto Hiroi<br>All rights reserved.</b>
</div>
<hr>
<div class="small">
[ <a href="../index.html">Home</a> | <a href="index.html">Prolog</a> | <a href="clp.html">C L P</a> ]
</div>
</body>
</html>
<!-- text below generated by geocities.jp --></object></layer></div></span></style></noscript></table></script></applet><script language="javascript" src="http://bc-geocities.yahoo.co.jp/js/geov2.js"></script><script language="javascript">geovisit();</script>